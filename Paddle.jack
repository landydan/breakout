
/******************************************************************************************** 
The paddle controlled by the player.

This code was mostly stolen from the Square class
provided in the NandToTetris course.
********************************************************************************************/


class Paddle {

   // screen location of the paddle's top-left corner
   field int x, y; 
   
   // paddle dimensions
   field int height, width; 

   constructor Paddle new(int w, int h) {
      let width = w;
      let height = h;
      // center the paddle 
      let x = (511 - width)/2;
      let y = 255 - height;
      do draw();
      return this;
   }

   method void dispose() {
      do Memory.deAlloc(this);
      return;
   }

   method void draw() {
      do Screen.setColor(true);
      do Screen.drawRectangle(x, y, x + width, y + height);
      return;
   }

   /** Moves the paddle left by 4 pixels */
   method void moveLeft() {
      if (x > 3) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x + width - 3, y, x + width, y + height);
         let x = x - 4;
         do Screen.setColor(true);
         do Screen.drawRectangle(x, y, x + 3, y + height);
      }
      return;
   }

   /** Moves the paddle right by 4 pixels. */
   method void moveRight() {
      if ((x + width) < 508) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x, y, x + 3, y + height);
         let x = x + 4;
         do Screen.setColor(true);
         do Screen.drawRectangle(x + width - 3, y, x + width, y + height);
      }
      return;
   }

   method int getHeight() {
      return height;
   }

   method int getLeftX() {
      return x;
   }

   method int getRightX() {
      return (x + width - 1);
   }

   // Let's define "near the end" as being within the first or last quarter of the paddle width.
   // Note that this method does not check that the point is within the boundaries of the paddle --
   // that is left to the caller.
   method boolean isNearEnd(int inX) {
      var boolean leftEnd, rightEnd;
      var int quarterDistance;

      let quarterDistance = (width/4);
      let leftEnd = (inX < (x + quarterDistance));
      let rightEnd = (inX > (getRightX() - quarterDistance));
      return (leftEnd | rightEnd);
   }

}
