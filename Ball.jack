
/******************************************************************************************** 
The game ball.

This is implemented by using the OS Screen.drawCircle() method.
A faster implementation might be to write directly into screen memory,
but this is quite tricky if the ball needs to support movement in various directions
by a single pixel at a time.
If the ball moved only in 16-pixel increments, a bit-map approach would work.
********************************************************************************************/
class Ball {

   field int x, y; 
   field int radius; 

   constructor Ball new(int Ax, int Ay, int Aradius) {
      let x = Ax;
      let y = Ay;
      let radius = Aradius;
      do draw();
      return this;
   }


   method void dispose() {
      do Memory.deAlloc(this);
      return;
   }


   method void draw() {
      do drawOrErase(true);
      return;
   }

   method void erase() {
      do drawOrErase(false);
      return;	  
   }

   method void drawOrErase(boolean color) {
      do Screen.setColor(color);
      do Screen.drawCircle(x, y, radius);
      return;
   }

   /** move by the specified x and y displacements */
   method void move(int xDelta, int yDelta) {
      var int newY;
      var int newX;
      do erase();
      let newY = y + yDelta;
      if ((newY > radius) & (newY < (256 - radius))) {
      	 let y = newY;
      }	      	   	  
      let newX = x + xDelta;
      if ((newX > radius) & (newX < (512 - radius))) {
      	 let x = newX;
      }
      do draw();
      return;      
   }

   method int getLeftX() {
      return (x - radius);
   }   

   method int getLeftY() {
      return y;
   }   

   method int getRightX() {
      return (x + radius);
   }   

   method int getRightY() {
      return y;
   }   

   method int getTopX() {
      return x;
   }

   method int getTopY() {
      return (y - radius);
   }

   method int getBottomX() {
      return x;
   }   

   method int getBottomY() {
      return (y + radius);	  
   }   


   method int getX() {
     return x; 	  
   }

   method int getY() {
     return y; 	  
   }

}
